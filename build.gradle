buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.flywaydb:flyway-mysql:11.10.4'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'org.springframework.boot'  version '3.5.4'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.diffplug.spotless' version '7.2.1'
    id 'com.netflix.dgs.codegen' version '8.1.1'
    id 'io.freefair.lombok' version '8.14'
    id 'org.flywaydb.flyway' version '11.10.4'
    id 'com.github.node-gradle.node' version '7.1.0'
}

group = 'org.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '24'

def npmExec = System.getProperty('os.name').toLowerCase().contains('windows') ? '/npm.cmd' : '/bin/npm'
def nodeExec = System.getProperty('os.name').toLowerCase().contains('windows') ? '/node.exe' : '/bin/node'

repositories {
    mavenCentral()
}

dependencies {
    implementation platform ('com.netflix.graphql.dgs:graphql-dgs-platform-dependencies:10.2.4')
    implementation 'com.netflix.graphql.dgs:dgs-starter'
    implementation 'com.mysql:mysql-connector-j:9.3.0'
    implementation 'org.springframework.boot:spring-boot-starter-web:3.5.4'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:3.5.4'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.testcontainers:testcontainers:1.21.3'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

generateJava {
    schemaPaths = ['src/main/resources/schema']
    packageName = 'app.generated'
    generateClient = true
}

node {
    download = true
    version = '22.20.0'
    npmVersion = '10.9.3'
}

spotless {
    java {
        target 'src/**/*.java'
        targetExclude '**/generated/**'
        importOrder()
        removeUnusedImports()
        removeWildcardImports()
        trimTrailingWhitespace()
        prettier(['prettier': '3.0.3', 'prettier-plugin-java': '2.3.0'])
                .npmExecutable("${tasks.named('npmSetup').get().npmDir.get()}${npmExec}")
                .nodeExecutable("${tasks.named('nodeSetup').get().nodeDir.get()}${nodeExec}")
                .config(['parser': 'java', 'tabWidth': 4, 'plugins': ['prettier-plugin-java']])
        formatAnnotations()
        endWithNewline()
    }
    sql {
        target 'src/main/resources/**/*.sql'
        trimTrailingWhitespace()
        prettier(['prettier': '3.0.3', 'prettier-plugin-sql': '0.19.2'])
                .npmExecutable("${tasks.named('npmSetup').get().npmDir.get()}${npmExec}")
                .nodeExecutable("${tasks.named('nodeSetup').get().nodeDir.get()}${nodeExec}")
                .config(['parser': 'sql', 'tabWidth': 4, 'keywordCase': 'upper', 'plugins': ['prettier-plugin-sql']])
        endWithNewline()
    }
    format 'graphql', {
        target 'src/main/resources/**/*.graphql'
        trimTrailingWhitespace()
        prettier(['prettier': '3.0.3'])
                .npmExecutable("${tasks.named('npmSetup').get().npmDir.get()}${npmExec}")
                .nodeExecutable("${tasks.named('nodeSetup').get().nodeDir.get()}${nodeExec}")
                .config(['parser': 'graphql', 'tabWidth': 4])
        endWithNewline()
    }
}

tasks.named('spotlessJava').configure {
    it.dependsOn('nodeSetup', 'npmSetup')
}

tasks.named('spotlessSql').configure {
    it.dependsOn('nodeSetup', 'npmSetup')
}

tasks.named('spotlessGraphql').configure {
    it.dependsOn('nodeSetup', 'npmSetup')
}

flyway {
    driver = 'com.mysql.cj.jdbc.Driver'
    url = System.getenv('DB_HOST') ?: 'jdbc:mysql://localhost:3306/personal_collection'
    user = System.getenv('DB_USER') ?: 'root'
    password = System.getenv('DB_PASSWORD') ?: 'password'
}

springBoot {
    mainClass = 'app.Application'
}

application {
    mainClass = 'app.Application'
}

test {
    useJUnitPlatform()
}
